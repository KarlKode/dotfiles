skip_global_compinit=1

# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/Code

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# cache certain commands
integer cache_reboot
if [ "$(uname)" = "Darwin" ]; then
  ((cache_reboot=`date +%s`-`sysctl kern.boottime | cut -c24-33`))
else
  ((cache_reboot=`date +%s`-`awk '{print $1}' /proc/uptime`))
fi
((cache_reboot=$cache_reboot/60))

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${${config_files:#*/path.zsh}:#*/completion.zsh}:#*/*.plugin.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion*.zsh}
do
  source $file
done

unset cache_reboot
unset config_files
unset skip_global_compinit

