autoload colors && colors
# cheers, @ehrenmurdick
# http://github.com/ehrenmurdick/config/blob/master/zsh/prompt.zsh

if (( $+commands[git] ))
then
  git="$commands[git]"
else
  git="/usr/bin/git"
fi

git_branch() {
  echo $($git symbolic-ref HEAD 2>/dev/null | awk -F/ {'print $NF'})
}

git_dirty() {
  if $(! $git status -s &> /dev/null)
  then
    echo ""
  else
    if [[ $($git status --porcelain) == "" ]]
    then
      echo "on %{$fg_bold[green]%}$(git_prompt_info)%{$reset_color%}"
    else
      echo "on %{$fg_bold[red]%}$(git_prompt_info)%{$reset_color%}"
    fi
  fi
}

git_prompt_info () {
 ref=$($git symbolic-ref HEAD 2>/dev/null) || return
# echo "(%{\e[0;33m%}${ref#refs/heads/}%{\e[0m%})"
 echo "${ref#refs/heads/}"
}

unpushed () {
  $git cherry -v @{upstream} 2>/dev/null
}

need_push () {
  if [[ $(unpushed) == "" ]]
  then
    echo " "
  else
    echo " with %{$fg_bold[magenta]%}unpushed%{$reset_color%} "
  fi
}

ruby_version() {
  if (( $+commands[rbenv] ))
  then
    echo "$(rbenv version | awk '{print $1}')"
  fi

  if (( $+commands[rvm-prompt] ))
  then
    echo "$(rvm-prompt | awk '{print $1}')"
  fi
}

rb_prompt() {
  if ! [[ -z "$(ruby_version)" ]]
  then
    echo "%{$fg_bold[yellow]%}$(ruby_version)%{$reset_color%} "
  else
    echo ""
  fi
}

python_virtualenv() {
  if [ $VIRTUAL_ENV ]
  then
    echo "`basename $VIRTUAL_ENV`"
  fi
}

python_version() {
  if (( $+commands[python] ))
  then
	  echo "$(python --version 2>&1| awk '{print $2}')"
  else
    echo ""
  fi
}

py_prompt() {
  if ! [[ -z "$(python_virtualenv)" ]]
  then
	  echo "%{$fg_bold[green]%}$(python_virtualenv) ($(python_version))%{$reset_color%} "
  else
    echo ""
  fi
}

bg_tasks() {
  local jobs_running
  jobs_running=$(jobs -l | sed -n '$=')
  if [[ $jobs_running -gt 0 ]]
  then
    echo " : %{$fg_bold[orange]%}${jobs_running}%{$reset_color%}"
  else
    echo ""
  fi
}

RETVAL=-1

symbols() {
  if [[ $RETVAL -ne 0 ]]
  then
    echo "%{$fg[red]%}¿%{$reset_color%}"
  elif [[ $UID -eq 0 ]]
  then
    echo "%{$fg[magenta]%}»%{$reset_color%}"
  else
    echo "›"
  fi
}

directory_name() {
  echo "%{$fg_bold[cyan]%}%1/%\/%{$reset_color%}"
}

export PROMPT=$'\n$(rb_prompt)$(py_prompt)in $(directory_name) $(git_dirty)$(need_push)$(bg_tasks)\n$(symbols) '
set_prompt () {
  export RPROMPT="%{$fg_bold[cyan]%}%{$reset_color%}"
}

precmd() {
  RETVAL=$?
  title "zsh" "%m" "%55<...<%~"
  set_prompt
}
